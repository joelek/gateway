guard ASN1BitString: ../asn1/BitString;
guard ASN1Integer: ../asn1/Integer;
guard ASN1Null: ../asn1/Null;
guard ASN1Node: ../asn1/Node;
guard ASN1ObjectIdentifier: ../asn1/ObjectIdentifier;
guard ASN1OctetString: ../asn1/OctetString;
guard ASN1Sequence: ../asn1/Sequence;
guard ASN1Set: ../asn1/Set;
guard ASN1UTF8String: ../asn1/UTF8String;
guard ASN1UTCTime: ../asn1/UTCTime;
guard ASN1Boolean: ../asn1/Boolean;

guard Version: ASN1Integer;

guard CertificateSerialNumber: ASN1Integer;

guard Validity: ASN1Sequence & {
	data: [
		ASN1UTCTime,
		ASN1UTCTime
	]
};

guard SubjectKeyIdentifierExtension: ../pkcs10/Extension & {
	data: [
		ASN1ObjectIdentifier & { data: "2.5.29.14" },
		ASN1OctetString
	]
};

guard AuthorityKeyIdentifierExtension: ../pkcs10/Extension & {
	data: [
		ASN1ObjectIdentifier & { data: "2.5.29.35" },
		ASN1OctetString
	]
};

guard BasicConstraintsExtension: ../pkcs10/Extension & {
	data: [
		ASN1ObjectIdentifier & { data: "2.5.29.19" },
		ASN1Boolean,
		ASN1OctetString
	]
};

guard TBSCertificate: ASN1Sequence & {
	data: [
		{
			kind: "CONTEXT",
			form: "CONSTRUCTED",
			type: "END_OF_CONTENT", # Context-specific type 0.
			data: [
				Version
			]
		},
		CertificateSerialNumber,
		../pkcs5/AlgorithmIdentifier,
		../pkcs10/Name,
		Validity,
		../pkcs10/Name,
		../pkcs8/PublicKeyInfo,
		# issuerUniqueID optional
		# subjectUniqueID optional
		{
			kind: "CONTEXT",
			form: "CONSTRUCTED",
			type: "BIT_STRING", # Context-specific type 3.
			data: [
				../pkcs10/Extensions
			]
		}
	]
};

guard Certificate: ASN1Sequence & {
	data: [
		TBSCertificate,
		../pkcs5/AlgorithmIdentifier,
		ASN1BitString
	]
};
