guard Account: {
	"contact"?: string[],
	"externalAccountBinding"?: {},
	"orders"?: string,
	"status": "valid" | "deactivated" | "revoked",
	"termsOfServiceAgreed"?: boolean
};

guard Authorization: {
	"identifier": Identifier,
	"status": "pending" | "valid" | "invalid" | "deactivated" | "expired" | "revoked",
	"expires"?: string,
	"challenges": Challenge[],
	"wildcard"?: boolean
};

guard Challenge: {
	"error"?: {},
	"status": "pending" | "processing" | "valid" | "invalid",
	"token": string,
	"type": string,
	"url": string,
	"validated"?: string
};

guard Directory: {
	"keyChange": string,
	"meta"?: {
		"caaIdentities"?: string[],
		"externalAccountRequired"?: boolean,
		"termsOfService"?: string,
		"website"?: string
	},
	"newAccount": string,
	"newAuthz"?: string,
	"newNonce": string,
	"newOrder": string,
	"revokeCert": string
};

guard Identifier: {
	"type": "dns",
	"value": string
};

guard Order: {
	"authorizations": string[],
	"certificate"?: string,
	"error"?: {},
	"expires"?: string,
	"finalize": string,
	"identifiers": Identifier[],
	"notBefore"?: string,
	"notAfter"?: string,
	"status": "pending" | "ready" | "processing" | "valid" | "invalid"
};

# ==============================================================================

guard Protected: {
	"nonce": string,
	"url": string
};

guard CreateAccountProtected: Protected & {
	"jwk": ../jwk/AssymetricKey
};

guard CreateAccountPayload: {
	"contact"?: string[],
	"termsOfServiceAgreed"?: boolean,
	"onlyReturnExisting"?: boolean,
	"externalAccountBinding"?: {}
};

guard CreateOrderProtected: Protected & {
	"kid": string
};

guard CreateOrderPayload: {
	"identifiers": Identifier[],
	"notBefore"?: string,
	"notAfter"?: string
};

# ==============================================================================

route getDirectory(): GET:/<path*>
	=> Directory;

route newAccount(): POST:/<path*>
	<= <{ "Content-Type" }> ../jws/Body
	=> <{ "Replay-Nonce", "Location" }> Account;

route newNonce(): HEAD:/<path*>
	=> <{ "Replay-Nonce" }>;

route newOrder(): POST:/<path*>
	<= <{ "Content-Type" }> ../jws/Body
	=> <{ "Replay-Nonce", "Location" }> Order;
